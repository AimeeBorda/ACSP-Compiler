 ---------------------------------------------
 --		Data Definitions
 ---------------------------------------------
 transparent normal

 datatype State = Before | During | After
 datatype IDS = H.{0..NumSections} | L.{0..NumSections} | VIP.{0..NumSections} | Corr  | Emp

 subtype SectionID = H.{0..NumSections} | L.{0..NumSections} | VIP.{0..NumSections}  | Corr
 subtype Agent = VIP.{0..NumSections} | H.{0..NumSections} | L.{0..NumSections}  | Emp

 s = VIP.0

 NumSections = 5 -- 35
 SectionSize = 5 -- 800 -- compiled in 667.77s (11 mins)
 ---------------------------------------------
 --		First-order events
 ---------------------------------------------

 channel goto : Agent.SectionID.SectionID
 channel event : State
 channel alarm :Bool
 channel wind
 channel noisy : Bool
 channel open : SectionID.SectionID
 channel lights : Bool
 channel empty



 Sensors = ((NoisePanel [|{|goto,event, open, empty|}|] AlarmPanel) [|{|event|}|] EventStatus)
 PhysicalController = (AccessController [|{|event|}|] EventStatus)
 Section = (PhysicalController [| {|goto,event, open, empty,noisy,alarm|}  |] Sensors)


---------------------------------------------
--		Adaptation Events
---------------------------------------------
channel adapt2 : Bool.Bool
channel adapt
channel adapt1 : Bool

channel ack

---------------------------------------------
--		Helper Functions
---------------------------------------------

t(x) = (x + 1) %NumSections
next(y.x) = y.t(x)
next(_) = Corr

r(x) = (x - 1) %NumSections
prev(y.x) = y.r(x)
prev(_) = Corr

notE = {x | x <- Agent, x != Emp}

adj = {next(s),prev(s),Corr}
sections = {y.z | y <- union({s},adj), z <- union({s},adj), y!=z,z == s or y ==s }

openEvt = {x.y | x <- SectionID, y <- SectionID, x == s or y ==s, x!=Corr, y != Corr}
monEvents = {| noisy,event,wind ,alarm |}
locEvents = union( {goto.y.x | x <- sections, y<- Agent} ,{open.x | x <- openEvt}	)
events = Union({locEvents, monEvents, {|empty|}})
adEvents = {|ack,adapt,adapt1|}

---------------------------------------------
--		Processes for A Section
---------------------------------------------

Behaviour(n) = (n > 0 & goto?_:{x | x<- Agent, x!= Emp}!s?_: adj -> Behaviour(n-1))
				[] (n < SectionSize & goto?_:{x | x<- Agent, x!= Emp}?_:adj!s -> Behaviour(n+1))
				[] (goto!Emp?_:sections -> Behaviour(n))
				[] (n == 0 & empty -> Behaviour(n))


SOpen = goto?_:{s,Emp}?_:adj!s -> SOpen [] goto?_:{s,Emp}!s?_ :adj -> SOpen
SExitAdj = goto!Emp?_:sections ->  SExit [] goto?_!s!Corr -> SExit [] goto?_?_:{next(s), prev(s)}!s -> SExit
SRoaming = goto?_?_:adj!s -> SRoaming	[] goto?_!s?_: adj -> SRoaming
SExit = goto!Emp?_:sections ->  SExit [] goto?_!s!Corr -> SExit


---------------------------------------------
--		Pattern for Req. A : event Pi (Before: ticketType)
---------------------------------------------

PA = let

		B = (event!Before  -> adapt -> ack -> B)
				[] (goto?_?_: sections -> B)
				[] (event?_:{During,After} -> B)
				[] (open?_ : openEvt -> B)
				[] (alarm?b ->  B)
				[] (empty -> B)
				[] (wind -> B)
				[] (noisy?_ -> B)
		within B

FA = adapt -> ac!<SOpen>.ack -> FA


---------------------------------------------
--		Pattern for Req. B : event Pi After
---------------------------------------------

PB = let B(aft,alrm) = (event!After  -> adapt1!False -> ack -> B(True,alrm))
				[] (goto?_?_: sections -> B(aft,alrm))
				[] (event!Before -> B(False,False))
				[] (event!During -> B(False,alrm))
				[] (open?_ : openEvt -> B(aft,alrm))
				[] (alarm?b -> B(aft,b))
				[] (empty -> if not alrm and aft then adapt1!True -> ack -> B(aft,alrm) else B(aft,alrm))
				[] (wind -> B(aft,alrm or member(s,{|H|})))
				[] (noisy?_ -> B(aft,alrm))
	within B(False,False)

FB = adapt1?emp -> if not emp then  ac!<(SExit ||| open!s?_ : {x | x <- adj, x!= Corr} -> SKIP)>.ack -> FB  else if  emp then ac!<(SExit ||| open?_ : {x | x <- adj, x!= Corr}!s -> SKIP)>. ack->FB else ack->FB

---------------------------------------------
--		Pattern for Req. C : alarm => exit
---------------------------------------------

PC = let B = (alarm!True  -> adapt-> ack -> B)
				[] (alarm!False -> B)
				[] (goto?_?_: sections -> B)
				[] (noisy?_ -> B)
				[] (event?_ -> B)
				[] (open?_ : openEvt -> B)
				[] (empty -> B)
				[] (wind -> B)
	within B

FC = adapt -> ac!<(SExit ||| open!s?_ : {x | x <- adj, x!= Corr} -> SKIP)>.ack -> FC

---------------------------------------------
--		Pattern for Req. D : during roam unless empty, alarm, wind, noisy
---------------------------------------------

PD = let
			applies(w, n, a,ev) = not (w or n or a) and ev == During
			B(wnd, n, alrm ,emp,ev) =
							(goto?_:notE?_:adj!s -> if applies(wnd,n,alrm,ev) then adapt1!False -> ack -> B(wnd, n, alrm ,False,ev) else B(wnd, n, alrm ,False,ev))
						[] (goto!Emp?_ : sections -> B(wnd, n, alrm, emp,ev))
						[] (goto?_:notE!s?_:adj -> B(wnd, n, alrm, emp,ev))
						[] (empty -> B(wnd, n, alrm, emp,ev))
						[] (open?_ :{x.y | x.y <-openEvt,y!=s} -> B(wnd, n, alrm, emp,ev))
						[] (open?_ :{x.y | x.y <-openEvt,y ==s} -> B(wnd, n, alrm, False,ev))
						[] (event!Before -> B(False, False,False,True,Before))
						[] (event!After -> B(wnd, n, alrm ,emp,After))
						[] (event!During -> if applies(wnd,n,alrm,ev) then  adapt1!emp -> ack -> B(wnd, n,alrm,emp,During) else B(wnd, n, alrm ,emp,During))

						[] (wind -> B(member(s, {|H|}), n, alrm,emp,ev))
						[] (alarm?b -> if applies(wnd,n,b,ev) then adapt1!emp -> ack -> B(wnd, n, b,emp,ev) else B(wnd,n,b,emp,ev))
						[] (noisy?b -> if applies(wnd,b,alrm,ev) then adapt1!emp -> ack -> B(wnd, b, alrm,emp,ev) else B(wnd,b,alrm,emp,ev))

		within B(False,False,False,True,Before)

FD = adapt1?emp:Bool -> if not emp or member(s,{|VIP|}) then ac!<SRoaming>.ack -> FD else if emp then ac!<(SOpen ||| (open?_ : {x | x <-SectionID, x!=s, x!=Corr}!s -> SKIP))>. ack -> FD else ack -> FD


---------------------------------------------
--		Pattern for Req. E : windy => (close H and open backup) or (ticketType)
---------------------------------------------

PE = let
			applies(alrm,ev) = (not alrm) and (ev == During)
			A(alrm,w,ev,ba) =
					(goto?_?_: sections -> A(alrm,w,ev,ba))
				[] (open!s?_ :{y | x.y <- openEvt,x==s, member(y,{|L|})} -> if applies(alrm,ev) and w then adapt1!True -> ack -> A(alrm,w,ev,True)else A(alrm,w,ev,True))
				[] (open?_ : {x.y | x.y <- openEvt, x != s or not member(y,{|L|})} -> A(alrm,w,ev,ba))
				[] (empty -> A(alrm,w,ev,ba))

				[] (event!Before -> A(False,False,Before,False))
				[] (event!During -> if applies(alrm,ev) and w then adapt1!False -> ack -> A(alrm,w,During,ba) else A(alrm,w,During,ba))
				[] (event!After -> A(alrm,w,After,ba))

				[] (wind -> if applies(alrm,ev) then adapt1!False -> ack -> A(alrm,True,ev,False) else A(alrm,True,ev,False))
				[] (alarm?b -> if applies(b,ev) and w then adapt1!ba -> ack -> A(b,w,ev,ba) else A(b,w,ev,ba))
				[] (noisy?_ -> A(alrm,w,ev,ba))
		within A(False,False,Before,False)

FE = adapt1?backup -> if (not backup and member(s, {|H|})) then ac!<(SOpen ||| (open!s?_ : {L.x, VIP.x | x <- {0..NumSections}} -> SKIP))>.ack -> FE else if (backup and member(s, {|H|})) then ac!<(SExit ||| open!s?_ : {x | x <- adj, x!= Corr} -> SKIP)>.ack -> FE else ack->FE

---------------------------------------------
--		Pattern for Req. F : if noisy by ticket type and open backup
---------------------------------------------

PF = let
			applies(alrm,w,ev) = not(alrm or w) and (ev == During)
			B(alrm,w,n,ev) = (goto?_ ?_: sections-> B(alrm,w,n,ev))
					[] (event!Before -> B(False,False,False,Before))
					[] (event!After -> B(alrm,w,n,After))
					[] (event!During -> B(alrm,w,n,During))
					[] (open?_ : openEvt -> B(alrm,w,n,ev))
					[] (noisy?b -> if applies(alrm,w,ev) and b then adapt -> ack -> B(alrm,w,b,ev) else B(alrm,w,b,ev))
					[] (wind -> B(alrm,member(s,{|H|}),n,ev))
					[] (alarm?b -> if applies(b,w,ev) and n then adapt -> ack -> B(b,w,n,ev) else B(b,w,n,ev))
					[] (empty -> B(alrm,w,n,ev))

		within B(False,False,False,Before)

FF = adapt -> ac!<(SOpen ||| (open!s?_ : {x | x <- SectionID, x!=s,x!=Corr}-> SKIP))>. ack -> FF

---------------------------------------------
--		Pattern for Req. G : empty can be used as a backup (empty event, no backup if wind,alarm)
---------------------------------------------

PG = let
		applies(alrm,w,n,ev) = not (alrm or w or n) and ev != Before
		B(alrm,w,n,e,ev) = (goto!Emp?_: adj!s -> B(alrm,w,n,e,ev))
				[] (goto?_:notE?_: adj!s -> B(alrm,w,n,False,ev))
				[] (goto?_!s?_: adj -> B(alrm,w,n,e,ev))
				[] (event!Before -> B(False,False,False,True,Before))
				[] (event!After -> B(alrm,w,n,e,After))
				[] (open?_ : {x.y | x.y <- openEvt, y != s} -> B(alrm,w,n,e,ev))
				[] (open?_ : {x.y | x.y <- openEvt, y == s} -> if applies(alrm,w,n,ev) and e then adapt1!ev == During -> ack -> B(alrm,w,n,False,ev) else B(alrm,w,n,False,ev))
				[] (wind -> B(alrm,member(s, {|H|}),n,e,ev))
				[] (noisy?b -> B(alrm,w,b,e,ev))
				[] (event!During -> B(alrm,w,n,e,During))
				[] (empty -> B(alrm,w,n,True,ev))
				[] (alarm?b ->  B(b,w,n,e,ev))

		within B(False,False,False,True,Before)

FG = adapt1?d -> if d then ac!<SRoaming>. ack -> FG else if not d then ac!<SExitAdj>. ack -> FG else ack -> FG


---------------------------------------------
--		Composition
---------------------------------------------

AccessController = let


		RA = (PA [| adEvents |] FA) \ adEvents
		RB = (PB [| adEvents |] FB) \ adEvents
		RC = (PC [| adEvents |] FC) \ adEvents
		RD = (PD [| adEvents |] FD) \ adEvents
		RE = (PE [| adEvents |] FE) \ adEvents
		RF = (PF [| adEvents |] FF) \ adEvents
		RG = (PG [| adEvents |] FG) \ adEvents

		C1 = (RA [| events |] RB)
		C2 = (C1 [| events |] RC)
		C3 = (C2 [| events |] RD)
		C4 = (C3 [| events |] RE)
		C5 = (C4 [| events |] RF)
		C6 = (C5 [| events |] RG)


		AdaptationProc = (C6 [|{|goto,empty|}|] Behaviour(0))

	within
			(new ac)(ac[SExit] [| locEvents |] AdaptationProc)



---------------------------------------------
--		Event Status
---------------------------------------------

EventStatus = event!Before -> event!During -> event!After -> EventStatus

---------------------------------------------
--		Light
---------------------------------------------
Lights_0 = lights!True -> STOP
Lights_1 = lights!False -> STOP
PI =
		let B(n,emp,ev) = (goto!Emp?_:sections -> B(n,emp,ev))

				[] (event!During-> adapt2!n!emp -> ack ->  B(n,emp,During))
				[] (event!Before -> adapt2!n!False -> ack -> B(n, True,Before))
				[] (event!After ->  B(n,emp,After))
				[] (empty -> if ev == After then adapt2!n!True -> ack -> B(n,True,ev) else B(n,emp,ev))

				[] (goto?_ :notE?_:adj!s -> if emp and ev != Before then adapt2!n!False -> ack -> B(n,False,ev) else B(n,False,ev))
				[] (goto?_ :notE!s?_:adj -> B(n,emp,ev))

				[] (open?_ : {x.y | x.y <- openEvt, y !=s} -> B(n,emp,ev))
				[] (open?_ : {x.y | x.y <- openEvt, y ==s} -> adapt2!n!False -> ack -> B(n,False,ev))
				[] (lights?b -> B(b,emp,ev))
			within B(False,True,Before)


FI = adapt2?l?emp -> if l and emp then lp!<Lights_1>. ack -> FI else if not l and not emp then lp!<Lights_0>.ack -> FI else lp!<STOP>.ack -> FI



LightsPanel = let
		AdaptationProc = (PI [| {|adapt2,ack|} |] FI) \ {|adapt2,ack|}
	within
		(new lp)(lp[STOP] [| {|lights|} |] AdaptationProc)

---------------------------------------------
--		Noise Panel
---------------------------------------------

Noisy_0 = noisy!False -> STOP
Noisy_1(b) = noisy!b -> Noisy_1(not b)

PH =
		let B(n,emp,ev) = (goto!Emp?_:sections -> B(n,emp,ev))
				[] (event!Before -> adapt2!n!True -> ack -> B(n,True,Before))
				[] (event!During -> if emp then adapt2!n!emp -> ack-> B(n,emp,During) else B(n,emp,During))
				[] (event!After -> adapt2!n!True-> ack -> B(n,emp,After))
				[] (noisy?b -> B(b,emp,ev))
				[] (empty -> if ev == During then adapt2!n!True -> ack ->  B(n,True,ev) else B(n,True, ev))
				[] (goto?_ :notE?_:adj!s -> if ev == During and emp then  adapt2!n!False -> ack->  B(n,False,ev)else B(n,False,ev))
				[] (goto?_ :notE!s?_:adj -> B(n,emp,ev))
				[] (open?_ :openEvt-> B(n,emp,ev))
			within  B(False,True,Before)

FH = adapt2?n?emp -> if emp and n then np!<Noisy_0>. ack -> FH
					else if not emp and n then np!<Noisy_1(False)>. ack -> FH
					else if not emp and not n then np!<Noisy_1(True)>.ack -> FH
					else if emp and not n then np!<STOP>. ack -> FH
					else ack -> FH


NoisePanel = let
		AdaptationProc = (PH [| {|adapt2,ack|} |] FH) \ {|adapt2,ack|}
	within
		(new np)(np[STOP] [| {|noisy|} |] AdaptationProc)


---------------------------------------------
--		Alarm Component
---------------------------------------------

Alarm_1(b) = alarm!b -> Alarm_1(not b)

AlarmPanel = Alarm_1(True)

include "assertions.acsp"
