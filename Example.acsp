datatype Connection = Secure | Open | HVAC

EventsH = {|connect.True.HVAC, connect.False.HVAC |}
Monitors3H = {|connect.True.Open,connect.False.Open,disconnect,update|}
Monitors6 = {|update|}
AdaptH = {|adapt,ack|}
MaxConnections = 3
Connections = {0 .. MaxConnections}

channel adapt : Connections.Bool
channel connect : Bool.Connection		-- agents' movement
channel disconnect
channel update
channel adapt6
channel ack
---------------------------------------------
--		Processes for HVAC
---------------------------------------------
H0 = connect!True!HVAC -> STOP
H1 = connect!False!HVAC -> STOP

---------------------------------------------
--		Pattern for HVAC
---------------------------------------------
Pattern3(n,b) =      (n == 0 & connect!True!Open -> adapt!1!b -> ack -> Pattern3(n+1,False))
				[] (n > 0 and n < MaxConnections & connect!True!Open -> Pattern3(n+1,b))
				[] (n == 1 & connect!False!Open -> adapt!n-1!b -> ack -> Pattern3(n-1,b))
				[] (n > 1 & connect!False!Open -> Pattern3(n-1,b))
				[] (disconnect -> adapt!0!b -> ack -> Pattern3(0,b))
				[] (connect?b':Bool!HVAC -> Pattern3(n,b'))
				[] (update -> Pattern3(n,b))
			--	[] (connect?_!Secure -> Pattern3(n,b))

Pattern6H =      (connect?_!HVAC -> Pattern6H)  [] (update -> adapt6 -> ack -> Pattern6H)

---------------------------------------------
--		Adaptation Function
---------------------------------------------
PiHvac =  adapt?v : Connections? h : Bool ->
						if v > 0 then l!<H1>.ack -> PiHvac
						else if v == 0 then l!<H0>.ack -> PiHvac
						else ack -> PiHvac
PiA6 =  adapt6 -> l!<H0>.ack -> PiA6
---------------------------------------------
--		Composition
---------------------------------------------
hvacComp = let
			R3H = (PiHvac [| {|adapt,ack |}|] Pattern3(0,True)) \ {|adapt,ack|}
			R6 = (PiA6 [|{| adapt6,ack |}|] Pattern6H) \ {|adapt6,ack|}
			AdptManager = R6 [|inter(Monitors6,Monitors3H)|] R3H
		within
			(new l)(l[H0] [| EventsH |] AdptManager)

---------------------------------------------
--		Assertions
---------------------------------------------
Requirement3 = let
					T(n) = (connect!False!Secure -> T(n))
							[] (connect!True!Secure-> T(0))
							[] (connect?_:Bool!HVAC -> T(n))
							[] (update -> T(n))
							[] (disconnect -> T(n))
							[] (n == 0 & connect!True!Open -> (	(connect!False!HVAC -> T(n+1))
																[] (connect!False!Open -> T(0))
																[] (connect!True!Secure -> T(n))
																[] (update -> T(n))
							))
							[] (n > 1 & connect!False!Open -> T(n-1))
							[] (n == 1 & connect!False!Open -> ((connect!True!HVAC -> T(n-1)) [] T(n-1)))
							[] (n < MaxConnections and n > 0 & connect!True!Open -> T(n+1))
					within T(0)

assert hvacComp :[deadlock free]
assert hvacComp  :[deterministic]
assert hvacComp  :[livelock free]

assert Requirement3 [T= hvacComp |\ EventsH